import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter
import java.time.format.FormatStyle

plugins {
    id("org.jetbrains.kotlin.jvm") version "${kotlinVersion}"
    id("org.jetbrains.kotlin.plugin.allopen") version "${kotlinVersion}"
    // If needs to add Serialization (to JSON, for example)
    // id("org.jetbrains.kotlin.plugin.serialization") version "${kotlinVersion}"
    id("com.google.devtools.ksp") version "${kotlinVersion}-1.0.13"
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "${micronautVersion}"
    id("gg.jte.gradle") version "${jteVersion}"
    // Provides better test output
    id("com.adarshr.test-logger") version "4.0.0"
    // Code Coverage:
    // https://github.com/Kotlin/kotlinx-kover
    id("org.jetbrains.kotlinx.kover") version "0.7.4"
    // Code Inspections
    // https://detekt.dev/
    id("io.gitlab.arturbosch.detekt") version("1.23.1")
    // Task graph utility
    // https://github.com/dorongold/gradle-task-tree
    id("com.dorongold.task-tree") version "2.1.1"
    // To generate a git.properties file containing git repository metadata
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    // Easily add new test sets
    id("org.unbroken-dome.test-sets") version "4.1.0"
}

ext {
    javaVersion = 17
    dockerImage = "ghcr.io/liber-ufpe/project-starter"
}

version = "0.1"
group = "br.ufpe.liber"

repositories {
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
        mavenContent { snapshotsOnly() }
    }
    mavenCentral()
}

application {
    mainClass.set("br.ufpe.liber.Application")
}

java {
    sourceCompatibility = JavaVersion.toVersion(javaVersion)
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion as int)
    }
}

test {
    useJUnitPlatform()
    // See https://kotest.io/docs/extensions/system_extensions.html#system-environment
    jvmArgs("--add-opens=java.base/java.util=ALL-UNNAMED")
}

testSets {
    accessibilityTest
}

tasks {
    dockerBuild {
        images = ["${dockerImage}:latest", "${dockerImage}:${project.version}", "${project.name}:latest", "${project.name}:${version}"]
    }

    dockerBuildNative {
        images = ["${dockerImage}:latest", "${dockerImage}:${project.version}", "${project.name}:latest", "${project.name}:${version}"]
    }
}

tasks.named("dockerfile") {
    // The default `openjdk` images are deprecated and not maintained.  Eclise Temurim
    // images are recommended instead:
    // https://hub.docker.com/_/eclipse-temurin
    baseImage = "eclipse-temurin:${javaVersion}-jdk-alpine"
}

tasks.named('dockerfileNative') {
    // Graal base images are now published here: https://github.com/graalvm/container
    // The `native-image-community` image is a size compact GraalVM Community Edition
    // container image with the Native Image support.	.
    graalImage = "ghcr.io/graalvm/native-image-community:${javaVersion}"
}

graalvmNative {
    toolchainDetection = false
    binaries {
        main {
            buildArgs.add('--verbose')
            // 7GB is what is available when using Github-hosted runners:
            // https://docs.github.com/en/actions/using-github-hosted-runners/about-github-hosted-runners#supported-runners-and-hardware-resources
            buildArgs.add('-J-Xmx7G')
            buildArgs.add('--initialize-at-build-time=kotlin.coroutines.intrinsics.CoroutineSingletons')
        }
    }
}

micronaut {
    runtime("netty")
    testRuntime("kotest5")
    processing {
        incremental(true)
        annotations("br.ufpe.*")
    }
}

jte {
    sourceDirectory = layout.projectDirectory.dir("src/main/jte").asFile.toPath()
    targetDirectory = layout.buildDirectory.dir("jte-classes").get().asFile.toPath()
    trimControlStructures = true
    packageName = "br.ufpe.liber"
    generate()
    jteExtension("gg.jte.models.generator.ModelExtension") {
        property("language", "Kotlin")
    }
}

tasks.configureEach {
    if (["kspKotlin", "inspectRuntimeClasspath"].contains(name)) {
        dependsOn "generateJte"
    }
}

jar {
    dependsOn precompileJte
    from fileTree("${layout.buildDirectory.asFile.get()}/jte-classes") {
        include "**/.*.class"
    }
}

testlogger {
    theme 'mocha'
    showExceptions true
    showStackTraces true
}

tasks.register("releaseDate") {
    doLast {
        println(
            ZonedDateTime
                .now()
                .format(
                    DateTimeFormatter
                        .ofLocalizedDateTime(FormatStyle.MEDIUM)
                        .withLocale(new Locale("pt-BR"))
                )
        )
    }
}

dependencies {
    ksp("io.micronaut.serde:micronaut-serde-processor")
    implementation("io.micronaut:micronaut-aop")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation("io.micronaut.kotlin:micronaut-kotlin-runtime")
    implementation("io.micronaut.kotlin:micronaut-kotlin-extension-functions")

    compileOnly("org.graalvm.nativeimage:svm")
    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("com.fasterxml.jackson.module:jackson-module-kotlin")

    // Views Dependencies
    implementation("gg.jte:jte-kotlin:${jteVersion}")
    implementation("gg.jte:jte-runtime:${jteVersion}")
    implementation("io.micronaut.views:micronaut-views-jte")
    jteGenerate "gg.jte:jte-models:${jteVersion}"

    implementation("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")

    // If needs to add Serialization (to JSON, for example). Also check the
    // plugins section.
    // implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0")

    // Netty security overrides
    implementation("io.netty:netty-handler:4.1.100.Final")
    implementation("io.netty:netty-codec-http2:4.1.100.Final")

    // Accessibility Tests
    accessibilityTestImplementation("org.seleniumhq.selenium:selenium-java:4.15.0")
    accessibilityTestImplementation("com.deque.html.axe-core:selenium:4.8.0")

    // Kotest manual update
    testImplementation("io.micronaut.test:micronaut-test-kotest5:4.1.0")
}